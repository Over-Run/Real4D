plugins {
    id 'java'
    id 'idea'
    id 'signing'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

group 'io.github.over-run'
archivesBaseName = 'real4d'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repo1.maven.org/maven2' }
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://jitpack.io' }
    // temporary maven repositories
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation 'io.github.over-run:glutils:1.3.0'
    implementation 'io.github.over-run:json:0.2.0'
    compileOnly 'org.jetbrains:annotations:22.0.0'
    runtimeOnly 'org.lwjgl:lwjgl::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-glfw::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-opengl::natives-windows'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from 'LICENSE'
    manifest {
        attributes 'Specification-Title': 'Real4D',
                'Specification-Vendor': 'Overrun Organization',
                'Specification-Version': '1',
                'Implementation-Title': 'Real4D',
                'Implementation-Vendor': 'Overrun Organization',
                'Implementation-Version': archiveVersion
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource, 'LICENSE', 'changelog.txt'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc, 'LICENSE', 'changelog.txt'
}

artifacts {
    archives javadocJar, sourcesJar
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing.publications {
    mavenJava(MavenPublication) {
        groupId = 'io.github.over-run'
        artifactId = 'real4d'
        version = project.version
        description = 'A sandbox game.'
        from components.java
        pom {
            name = 'Real4D'
            description = 'A sandbox game.'
            url = 'https://github.com/Over-Run/Real4D'
            licenses {
                license {
                    name = 'The MIT License'
                    url = 'https://raw.githubusercontent.com/Over-Run/Real4D/0.x/LICENSE'
                }
            }
            developers {
                developer {
                    id = 'squid233'
                    name = 'squid233'
                    email = '513508220@qq.com'
                }
            }
            scm {
                connection = 'https://github.com/Over-Run/Real4D.git'
                developerConnection = 'https://github.com/Over-Run/Real4D.git'
                url = 'https://github.com/Over-Run/Real4D'
            }
        }
    }
}
// You should add 'OSSRH_USERNAME' and 'OSSRH_PASSWORD'
// to environment path
// You should add 'signing.keyId', 'signing.password'
// and 'signing.secretKeyRingFile'
// to GRADLE_USER_HOME/gradle.properties
publishing.repositories {
    maven {
        name = "OSSRH"
        credentials {
            username System.getenv("OSSRH_USERNAME")
            password System.getenv("OSSRH_PASSWORD")
        }
        url = version.endsWith('-SNAPSHOT')
                ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
}

signing {
    sign publishing.publications.mavenJava
}

idea.module.inheritOutputDirs = true
