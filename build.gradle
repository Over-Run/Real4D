plugins {
    id 'application'
    id 'idea'
    id 'signing'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

group projGroupId
archivesBaseName = projArtifactId
version = projVersion

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://jitpack.io' }
    // temporary maven repositories
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation 'io.github.over-run:glutils:2.0.0-SNAPSHOT'
    implementation 'com.google.code.gson:gson:2.8.9'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    def nativeAttrib = [
        'linux-arm32', 'linux-arm64', 'linux',
        'macos-arm64', 'macos',
        'windows-arm64', 'windows-x86', 'windows'
    ]
    for (def attrib : nativeAttrib) {
        runtimeOnly "org.lwjgl:lwjgl::natives-$attrib"
        runtimeOnly "org.lwjgl:lwjgl-jemalloc::natives-$attrib"
        runtimeOnly "org.lwjgl:lwjgl-glfw::natives-$attrib"
        runtimeOnly "org.lwjgl:lwjgl-opengl::natives-$attrib"
        runtimeOnly "org.lwjgl:lwjgl-stb::natives-$attrib"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'org.overrun.real4d.client.main.Main'
}

jar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'
    from 'LICENSE', 'changelog.txt'
    manifest.attributes(
        'Main-Class': 'org.overrun.real4d.client.main.Main',
        'Specification-Title': projName,
        'Specification-Vendor': 'Overrun Organization',
        'Specification-Version': '0',
        'Implementation-Title': projName,
        'Implementation-Vendor': 'Overrun Organization',
        'Implementation-Version': archiveVersion
    )
}

javadoc {
    //-encoding utf-8 -charset utf-8 -windowtitle "Real4D x.y.z Javadoc" -link https://docs.oracle.com/en/java/javase/17/docs/api/
    failOnError = false
    options.encoding 'UTF-8'
    options.charSet 'UTF-8'
    options.author true
    options.links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
    options.windowTitle "Real4D $projVersion Javadoc"
}

/*task deployJar(type: Jar, dependsOn: build) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}*/

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource, 'LICENSE', 'changelog.txt'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc, 'LICENSE', 'changelog.txt'
}

artifacts {
    archives javadocJar, sourcesJar
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing.publications {
    mavenJava(MavenPublication) {
        groupId = projGroupId
        artifactId = projArtifactId
        version = projVersion
        description = projDesc
        from components.java
        pom {
            name = projName
            description = projDesc
            url = "https://github.com/$projVcs"
            licenses {
                license {
                    name = 'The MIT License'
                    url = "https://raw.githubusercontent.com/$projVcs/$projBranch/LICENSE"
                }
            }
            developers {
                developer {
                    id = 'squid233'
                    name = 'squid233'
                    email = '513508220@qq.com'
                }
            }
            scm {
                connection = "https://github.com/${projVcs}.git"
                developerConnection = "https://github.com/${projVcs}.git"
                url = "https://github.com/$projVcs"
            }
        }
    }
}
// You should add 'OSSRH_USERNAME' and 'OSSRH_PASSWORD'
// to environment path
// You should add 'signing.keyId', 'signing.password'
// and 'signing.secretKeyRingFile'
// to GRADLE_USER_HOME/gradle.properties
publishing.repositories {
    maven {
        name = "OSSRH"
        credentials {
            username System.getenv("OSSRH_USERNAME")
            password System.getenv("OSSRH_PASSWORD")
        }
        url = version.endsWith('-SNAPSHOT')
            ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
}

signing {
    sign publishing.publications.mavenJava
}

idea.module.inheritOutputDirs = true
